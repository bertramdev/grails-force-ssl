buildscript {
    ext {
      grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "io.github.gradle-nexus:publish-plugin:1.0.0"
    }
}


version "4.0.0"
group "com.bertramlabs.plugins"

apply plugin: "org.grails.grails-plugin"
// apply plugin: "org.grails.grails-gsp"
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "org.grails.grails-plugin"

ext {
  grailsVersion = project.grailsVersion
  isBuildSnapshot = version.endsWith('-SNAPSHOT')
  isReleaseVersion = !isBuildSnapshot
}

sourceCompatibility = 1.7
targetCompatibility = 1.7


repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://repo.grails.org/grails/core" }
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-logging'
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-autoconfigure"
  implementation "org.springframework.boot:spring-boot-starter-tomcat"

  implementation "org.grails:grails-web-boot"
  implementation "org.grails:grails-dependencies"
  implementation 'javax.servlet:javax.servlet-api:3.1.0'

  testCompile "org.grails:grails-plugin-testing"

  console "org.grails:grails-console"
}



if (isReleaseVersion) {
    apply plugin: "io.github.gradle-nexus.publish-plugin"
    nexusPublishing {
        repositories {
            sonatype {
                if(project.hasProperty('mavenUser')) {
                    username = mavenUser
                    password = mavenPassword
                }    
            }
        }
    }
} else {

}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'grails-force-ssl'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'grails-force-ssl'
                    description 'Grails Force SSL Interceptor and Controller Annotation.'
                    url 'https://github.com/bertramdev/grails-force-ssl'
                    scm {
                        url 'https://github.com/bertramdev/grails-force-ssl'
                        connection 'scm:https://bertramdev@github.com/bertramdev/grails-force-ssl.git'
                        developerConnection 'scm:git://github.com/bertramdev/grails-force-ssl.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            if( !isReleaseVersion ) {
                url = "http://nexus.bertramlabs.com/content/repositories/snapshots"
                if(project.hasProperty('labsNexusUser')) {
                    credentials {
                        username = labsNexusUser
                        password = labsNexusPassword
                    }
                }
            }
        }
    }
}



afterEvaluate {
    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.maven
    }
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

//do not generate extra load on Nexus with new staging repository if signing fails
tasks.withType(io.github.gradlenexus.publishplugin.InitializeNexusStagingRepository).configureEach {
    shouldRunAfter(tasks.withType(Sign))
}



